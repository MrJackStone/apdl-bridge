!/input,'F:\Ansys\03 Design\30 Pontes Anchieta\01 APDL\_slab_deflects',mac

windefa,1
/device,font,3,Arial,1000,0,-64,0,0,,,
/device,font,1,Arial,1000,0,-42,0,0,,,
/device,text,1,100
/tspec,15,1.5,4,0,0
/annot,dele
wincln,1
/udoc,1,cntr,right
/plopts,info,3
/plopts,leg3,1
/gformat,f,10,2
/annot,dele

!! Create element/node selection components for each 'span'                                       !!
*do,nthSpan,1,GEO_girderCount-1,1
    csA=CS_girders(nthSpan)
    csB=CS_girders(nthSpan+1)
    cmsel,s,COMPE_Slab
    nsle,s,active

    csys,csA
    nsel,r,loc,y,0,1e9

    csys,csB
    nsel,r,loc,y,-1e9,0

    esln,r,1
    cm,COMPE_SlabSpan_S%nthSpan%,elem
    cm,COMPN_SlabSpan_S%nthSpan%,node
*enddo

! Right overhang
cmsel,s,COMPE_Slab
nsle,s,active

csys,CS_rightDeckEdge
nsel,r,loc,y,0,1e9

csys,CS_girders(1)
nsel,r,loc,y,-1e9,0

esln,r,1
cm,COMPE_SlabSpab_SR,elem
cm,COMPN_SlabSpab_SR,node

! Left overhang
cmsel,s,COMPE_Slab
nsle,s,active

csys,CS_leftDeckEdge
nsel,r,loc,y,0,-1e9

csys,CS_girders(GEO_girderCount)
nsel,r,loc,y,0,1e9

esln,r,1
cm,COMPE_SlabSpab_SL,elem
cm,COMPN_SlabSpab_SL,node

!! Create paths                                                                                   !!
pathCount=50
cmsel,s,COMPE_Slab
nsle,s,active
*get,xmin,node,,mnloc,x
*get,xmax,node,,mxloc,x
xstep=(xmax-xmin)/(pathCount-1)
*del,x_path
*dim,x_path,,pathCount
*vfill,x_path,ramp,xmin,xstep

totalPathCount=(GEO_girderCount+1)*pathCount
*del,all_path_names
*dim,all_path_names,char,totalPathCount
*del,all_path_labels
*dim,all_path_labels,char,totalPathCount
pathIdx=0

*do,nthSpan,1,GEO_girderCount-1,1
    cmsel,s,COMPE_SlabSpan_S%nthSpan%
    cmsel,s,COMPN_SlabSpan_S%nthSpan%

    csA=CS_girders(nthSpan)
    *get,csAy,cdsy,csA,loc,y
    *get,csAxy,cdsy,csA,ang,xy

    csB=CS_girders(nthSpan+1)
    *get,csBy,cdsy,csB,loc,y
    *get,csBxy,cdsy,csB,ang,xy


    *do,nthPath,1,pathCount,1
        x=x_path(nthPath)
        *afun,deg
        yA=csAy+x*tan(csAxy)
        yB=csBy+x*tan(csBxy)
        *afun,rad

        pthName='S%nthSpan%_P%nthPath%'
        pathIdx=pathIdx+1
        all_path_names(pathIdx)=pthName
        all_path_labels(pathIdx)='NET_UZ'

        path,pthName,2,30,20
        ppath,1,,x,yA,0
        ppath,2,,x,yB,0
        pdef,'UZ',u,z,1
    *enddo
*enddo

! Right overhang paths
cmsel,s,COMPE_SlabSpab_SR
cmsel,s,COMPN_SlabSpab_SR

csA=CS_rightDeckEdge
*get,csAy,cdsy,csA,loc,y
*get,csAxy,cdsy,csA,ang,xy

csB=CS_girders(1)
*get,csBy,cdsy,csB,loc,y
*get,csBxy,cdsy,csB,ang,xy

*do,nthPath,1,pathCount,1
  x=x_path(nthPath)
  *afun,deg
  yA=csAy+x*tan(csAxy)
  yB=csBy+x*tan(csBxy)
  *afun,rad

  pthName='SR_P%nthPath%'
  pathIdx=pathIdx+1
  all_path_names(pathIdx)=pthName
  all_path_labels(pathIdx)='NET_UZ'

  path,pthName,2,30,20
  ppath,1,,x,yA,0
  ppath,2,,x,yB,0
  pdef,'UZ',u,z,1
*enddo

! Left overhang paths
cmsel,s,COMPE_SlabSpab_SL
cmsel,s,COMPN_SlabSpab_SL

csA=CS_girders(GEO_girderCount)
*get,csAy,cdsy,csA,loc,y
*get,csAxy,cdsy,csA,ang,xy

csB=CS_leftDeckEdge
*get,csBy,cdsy,csB,loc,y
*get,csBxy,cdsy,csB,ang,xy

*do,nthPath,1,pathCount,1
  x=x_path(nthPath)
  *afun,deg
  yA=csAy+x*tan(csAxy)
  yB=csBy+x*tan(csBxy)
  *afun,rad

  pthName='SL_P%nthPath%'
  pathIdx=pathIdx+1
  all_path_names(pathIdx)=pthName
  all_path_labels(pathIdx)='NET_UZ'

  path,pthName,2,30,20
  ppath,1,,x,yA,0
  ppath,2,,x,yB,0
  pdef,'UZ',u,z,1
*enddo

!! Calculate path envelopes                                                                       !!
*del,slab_envelope,,nopr
*dim,slab_envelope,char,2,totalPathCount
*do,nthPath,1,totalPathCount,1
  slab_envelope(1,nthPath)=all_path_names(nthPath)
  slab_envelope(2,nthPath)=all_path_labels(nthPath)
*enddo

pthenv,'CLEAR','slab_envelope'

*do,nthLS,1,LoadStepCount,1

  cmsel,s,COMPE_Slab
  nsle,s,active
  subset,nthLS,last

  ! Inner spans
  *do,nthSpan,1,GEO_girderCount-1,1
    cmsel,s,COMPE_SlabSpan_S%nthSpan%
    cmsel,s,COMPN_SlabSpan_S%nthSpan%
    *do,nthPath,1,pathCount,1
      pthName='S%nthSpan%_P%nthPath%'
      path,pthName
      pdef,'UZ',u,z,1
      pthrebase,'UZ',0,'NET_UZ', 1e3
    *enddo
  *enddo

  ! Right overhang paths
  cmsel,s,COMPE_SlabSpab_SR
  cmsel,s,COMPN_SlabSpab_SR
  *do,nthPath,1,pathCount,1
    pthName='SR_P%nthPath%'
    path,pthName
    pdef,'UZ',u,z,1
    pthrebase,'UZ',2,'NET_UZ', 1e3
  *enddo

  ! Left overhang paths
  cmsel,s,COMPE_SlabSpab_SL
  cmsel,s,COMPN_SlabSpab_SL
  *do,nthPath,1,pathCount,1
    pthName='SL_P%nthPath%'
    path,pthName
    pdef,'UZ',u,z,1
    pthrebase,'UZ',1,'NET_UZ', 1e3
  *enddo

  pthenv,'UPDATE','slab_envelope'
*enddo

!! Plot path items                                                                                !!
*del,max_envelope_labels,,nopr
*dim,max_envelope_labels,char,totalPathCount
*del,min_envelope_labels,,nopr
*dim,min_envelope_labels,char,totalPathCount
*do,nthPath,1,totalPathCount,1
  max_envelope_labels(nthPath)='NET_UZ+'
  min_envelope_labels(nthPath)='NET_UZ-'
*enddo

cmsel,s,COMPE_Slab
nsle,s,active
/window,all,dele
/window,1,-1,1.67,0,1
/window,2,-1,1.67,-1,0
/auto,all
/angle,all,0
! /view,1,1,1,1
/view,1,1,0,0
/view,2,1,0,0
/eshape,0
/edge,all,1

/annot,dele
wntitle , 'LAJE TABULEIRO'   , 1 , 0
wntitle , 'FLECHA MAX. [mm]' , 1 , 1 , 'R'
wntitle , 'LAJE TABULEIRO'   , 2 , 0
wntitle , 'FLECHA MIN. [mm]' , 2 , 1 , 'R'

go2png,1200
  /window,1,on
  /window,2,on
  cmsel,s,COMPE_Slab
  cmsel,a,COMPE_girder
  nsle,s,active
  /eshape,1
  /gline,all,-1
  eplot
  /user,all
  /noerase
  cmsel,s,COMPE_Slab
  nsle,s,active
  /eshape,0
  ! /gline,all,0

  /window,1,on
  /window,2,off
  mpathplot,5,,'all_path_names','max_envelope_labels'

  /window,1,off
  /window,2,on
  mpathplot,5,,'all_path_names','min_envelope_labels'

  /gline,all,0

endpng,'slab_deflections',,,,,'memo_imgs'
/erase
/annot,dele
