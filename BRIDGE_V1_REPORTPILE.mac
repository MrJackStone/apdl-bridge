! BRIDGE_V1_REPORTPILE

cmsel,s,COMPL_pile
cmsel,a,COMPL_ABT_piles
! cmsel,a,COMPL_column
cm,COMPL_allPiles,line
*get,lc,line,,count

! cmsel,s,compn_pile
! esln,s
! esel,u,type,,3
cmsel,s,COMPL_allPiles
esll,s
cm,COMPE_allPiles,elem
*get , ec , elem ,  , count

*del , nd_all
*del , nk_all
*del , md_all
*del , mk_all
*del , z_all
*del , i_all
*dim , nd_all ,  , ec
*dim , nk_all ,  , ec
*dim , md_all ,  , ec
*dim , mk_all ,  , ec
*dim , z_all  ,  , ec
*dim , i_all  ,  , ec

enum=0
*do,ei,1,ec,1
  cmsel,s,COMPE_allPiles
  nsle,s,active

  enum=elnext(enum)

  elemCenterZ=centrz(enum)

  pileLine=0
  lnum=0
  *do,nthLine,1,lc,1
    cmsel,s,COMPL_allPiles
    lnum=lsnext(lnum)
    lsel,s,line,,lnum
    esll,s
    selCheck=esel(enum)
    *if,selCheck,eq,1,then
      pileLine=lnum
      *exit
    *endif
  *enddo

  zmax=0
  *if,pileLine,ne,0,then
    lsel,s,line,,pileLine
    nsll,s
    cmsel,r,COMPN_pile
    *get,zmax,node,,mxloc,z
  *endif
  elemDepth=zmax-elemCenterZ

  *get,nth_nd,elem,enum,etab,N_cd_est
  *get,nth_nk,elem,enum,etab,N_cd_geo

  *get,nth_md,elem,enum,etab,M_od_est
  *get,nth_mk,elem,enum,etab,M_od_geo

  i_all(ei)=pileLine
  z_all(ei)=elemDepth

  nk_all(ei)=nth_nk
  nd_all(ei)=nth_nd
  mk_all(ei)=nth_mk
  md_all(ei)=nth_md
*enddo

*cfopen,'RES_pileSects_MN',txt
    *vwrite,
    ('------------Geometria  ------------ELU-Estrutural  -------------ELU-Geotecnia')
    *vwrite,
    (' PILE #         Z [m]        N [kN]       M [kNm]        N [kN]       M [kNm]')
    *vwrite,i_all(1),z_all(1),nd_all(1),md_all(1),nk_all(1),mk_all(1)
    (F8.0,TL1,'  ',F12.2,'  ',F12.2,'  ',F12.2,'  ',F12.2,'  ',F12.2,'  ',F12.2,'  ',F12.2)
*cfclos

!!                                                                                                !!
/post1
set,1

*del,pilegroup_i
*del,pilegroup_m
*del,pilegroup_n
*del,pilegroup_v
*dim,pilegroup_i,,GEO_supportCount
*dim,pilegroup_m,,GEO_supportCount
*dim,pilegroup_n,,GEO_supportCount
*dim,pilegroup_v,,GEO_supportCount

*vfill,pilegroup_i,ramp,1,1

*do,nthSup,1,GEO_supportCount,1

    x_i=GEO_supportX(nthSup)

    *if,nthSup,eq,1,then
        ! First abutment
        x_j=x_i-GEO_ABT1_pileCapL
        *if,GEO_ABT1_type,eq,4,then
            x_i=x_i-GEO_ABT1_pileCapW/2
            x_j=x_i+GEO_ABT1_pileCapW
        *endif

    *elseif,nthSup,eq,GEO_genSpanCount,then
        ! Last abutment
        x_j=x_i+GEO_ABT2_pileCapL
        *if,GEO_ABT2_type,eq,4,then
            x_i=x_i-GEO_ABT2_pileCapW/2
            x_j=x_i+GEO_ABT2_pileCapW
        *endif

    *else
        ! Tubular pier columns
        x_i=x_i-GEO_capLength/2
        x_j=x_i+GEO_capLength

    *endif

    cmsel,s,COMPL_pile
    cmsel,a,COMPL_ABT_piles
    ksll,s
    ksel,r,loc,x,x_i,x_j
    lslk,r,0
    ksll,s
    esll,s
    nsle,s,active

    cmeName=strcat('COMPE_PILE_sup',chrval(nthSup))
    cmlName=strcat('COMPL_PILE_sup',chrval(nthSup))
    cm,%cmeName%,elem
    cm,%cmlName%,line

    pletab,'M_obl'
    *get,m_mx,plnsol,,max
    pletab,'N_c'
    *get,n_mx,plnsol,,min
    pletab,'Q_obl'
    *get,v_mx,plnsol,,max

    pilegroup_m(nthSup)=m_mx
    pilegroup_n(nthSup)=n_mx
    pilegroup_v(nthSup)=v_mx

*enddo


*cfopen,'RES_pileGroup',txt
    *vwrite,
    (' SUPPORT #          NSd [kN]         MSd [kNm]          VSd [kN]')
    *vwrite,pilegroup_i(1),pilegroup_n(1),pilegroup_m(1), pilegroup_v(1)
    (f11.0,tl1,'   ',f15.2,'   ',f15.2,'   ',f15.2)
*cfclos



*cfopen,'RES_piles_grouped',txt
    *vwrite
    ('PILES GROUPED BY SUPPORT')
    *vwrite
    (' ')
    *vwrite
    (' ')
    *vwrite
    ('UNITS: kN, kNm')
    *vwrite
    (' ')
    *vwrite
    (' ')
    *vwrite
    ('______________________________________________________')
*cfclos


*do,nthSup,1,GEO_supportCount,1

    cmlName=strcat('COMPL_PILE_sup',chrval(nthSup))
    cmsel,s,%cmlName%

    *get,lc,line,,count
    *del,pile_m
    *del,pile_n
    *del,pile_v
    *del,pile_x
    *del,pile_y
    *dim,pile_m,,lc
    *dim,pile_n,,lc
    *dim,pile_v,,lc
    *dim,pile_x,,lc
    *dim,pile_y,,lc
    ln=0
    *do,nthLine,1,lc,1
        cmsel,s,%cmlName%
        ln=lsnext(ln)
        lsel,s,line,,ln
        esll,s
        nsle,s,active

        pletab,'M_obl'
        *get,m_mx,plnsol,,max
        pletab,'N_c'
        *get,n_mx,plnsol,,min
        pletab,'Q_obl'
        *get,v_mx,plnsol,,max

        *get,ztop,node,,mxloc,z
        nsel,r,loc,z,ztop
        topNode=ndnext(0)
        xtop=nx(topNode)
        ytop=ny(topNode)

        pile_m(nthLine)=m_mx
        pile_n(nthLine)=n_mx
        pile_v(nthLine)=v_mx
        pile_x(nthLine)=xtop
        pile_y(nthLine)=ytop
    *enddo


    *cfopen,'RES_piles_grouped',txt,,append
        *vwrite,nthSup
        ('SUPPORT NUMBER: ',f3.0,tl1,' ')
        *vwrite,
        (' ')
        *vwrite,
        ('              X                Y              NSd              MSd                   VSd')
        *vwrite,pile_x(1),pile_y(1),pile_n(1),pile_m(1),pile_v(1)
        (F15.4,'  ',F15.4,'  ',F15.2,'  ',F15.2,'  ',F20.2)
        *vwrite
        (' ')
        *vwrite
        ('______________________________________________________')
    *cfclos


*enddo
