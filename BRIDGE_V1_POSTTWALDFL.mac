! BRIDGE_V1_POSTTWALDFL , abtNum , dflType
! BRIDGE_V1_POSTTWALDFL , arg1   , arg2

!! Initial configs                                                                                !!
windefa,1
/device,font,3,Arial,1000,0,-64,0,0,,,
/device,font,1,Arial,1000,0,-42,0,0,,,
/device,text,1,100
/tspec,15,1.5,4,0,0
/gformat,f,10,2
/annot,dele

pathCount=5

cmsel,s,COMPA_ABT_tranwall
cmsel,r,COMPA_ABT_%arg1%
esla,s
nsle,s,active

csys,CS_ABT%arg1%
*get,ymin,node,,mnloc,y
*get,ymax,node,,mxloc,y
csys,0

yStep=(yMax-yMin)/(pathCount-1)
zLocal1=0
zLocal2=-GEO_ABT%arg1%_height

*del , path_names  ,      , nopr
*dim , path_names  , char , pathCount
*del , path_labels ,      , nopr
*dim , path_labels , char , pathCount

!! Create paths                                                                                   !!
*do,nthPath,1,pathCount,1
  yLocal=yMin+yStep*(nthPath-1)
  loc2glo,0,yLocal,zLocal1,CS_ABT%arg1%,'x1g','y1g','z1g'
  loc2glo,0,yLocal,zLocal2,CS_ABT%arg1%,'x2g','y2g','z2g'

  pathName='TRW_P%nthPath%'
  path_names(nthPath)=pathName
  path_labels(nthPath)='NET_UX'

  path,pathName,2,30,50
  ppath,1,,x1g,y1g,z1g
  ppath,2,,x2g,y2g,z2g
  pdef,'UX',u,x,avg
*enddo

!! Calculate path envelopes                                                                       !!
*del,twall_envelope,,nopr
*dim,twall_envelope,char,2,pathCount
*do,nthPath,1,pathCount,1
  twall_envelope(1,nthPath)=path_names(nthPath)
  twall_envelope(2,nthPath)=path_labels(nthPath)
*enddo

pthenv,'CLEAR','twall_envelope'

cmsel,s,COMPA_ABT_tranwall
cmsel,r,COMPA_ABT_%arg1%
esla,s
nsle,s,active

*do,nthLS,1,LoadStepCount,1

  subset,nthLS,last

  *do,nthPath,1,pathCount,1
    pathName='TRW_P%nthPath%'
    path,pathName
    pdef,'UX',u,x,avg
    pthrebase,'UX',arg2,'NET_UX',1e3/1.4 !! type 0 or 2
  *enddo

  pthenv,'UPDATE','twall_envelope'
*enddo

!! Plot path envelopes                                                                            !!
*del , MPATHPLOT_ARR_pathNames ,      , nopr
*dim , MPATHPLOT_ARR_pathNames , char , pathCount*2
*del , MPATHPLOT_ARR_labels    ,      , nopr
*dim , MPATHPLOT_ARR_labels    , char , pathCount*2

*do,nthPath,1,pathCount,1
  MPATHPLOT_ARR_pathNames(nthPath*2-1) = path_names(nthPath)
  MPATHPLOT_ARR_pathNames(nthPath*2)   = path_names(nthPath)
  MPATHPLOT_ARR_labels(nthPath*2-1)    = 'NET_UX+'
  MPATHPLOT_ARR_labels(nthPath*2)      = 'NET_UX-'
*enddo

view1w
wincln,1
clims

*if,arg1,eq,1,then
  /view,1,1,1,1
*elseif,arg1,eq,2,then
  /view,1,-1,-1,1
*endif

/angle,1,0
/udoc,1,cntr,right
/plopts,leg3,on
wntitle , 'ENCONTRO 0%arg1%:'  , 1 , 0
wntitle , 'PAREDE TRANSVERSAL' , 1 , 0 , 'C'
wntitle , 'FLECHA RARA [mm]'   , 1 , 1 , 'R'
/triad,off
/eshape,1
/udoc,1,cntr,right
/edge,1,0
cmsel,s,COMPA_ABT_tranwall
cmsel,r,COMPA_ABT_%arg1%
/auto,1
aplot
/user,1
cmsel,s,COMPA_ABT_%arg1%
cmsel,u,COMPA_ABT_tranwall
esla,s
nsle,s,active

go2png,1200
  /device,vector,on
  eplot
  /user,1
  /noerase
  cmsel,s,COMPL_ABT_piles
  esll,a
  nsle,s,active
  eplot
  cmsel,s,COMPA_ABT_tranwall
  cmsel,r,COMPA_ABT_%arg1%
  esla,s
  nsle,s,active
  /device,vector,off
  /eshape,0
  /edge,1,0
  eplot
  cleanplot,'mpathplot','3'
endpng,'IMG19a_ABT%arg1%_TRANWALL_deflects',,,,,POST_resultsFolder
/erase
/annot,dele
clims
