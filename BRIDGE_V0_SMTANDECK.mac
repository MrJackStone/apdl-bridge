! BRIDGE_V0_SMTANDECK

blc4,0,-GEO_deckWidth/2,GEO_deckLength,GEO_deckWidth,0

! Cut spans
*do,nthSupport,2,GEO_supportCount-1,1 !!,1,GEO_supportCount,1
    allsel
    xCoord=GEO_supportX(nthSupport)
    zCoord=GEO_frameTopOffset(nthSupport)
    wpsliceyz,xCoord,1
    *if,zCoord,ne,0,then
        ksel,r,loc,x,xCoord
        kmodif,all,,,zCoord
    *endif
*enddo

! Cut at midspans
allsel
batch_sliceyz,'GEO_midspanX'

! Cut at gerber connections
*if,GEO_gerberStatus,eq,1,then
    batch_sliceyz,'GEO_gerberX_i',,1
    batch_sliceyz,'GEO_gerberX_j',,1
*endif

! Cut at tubular column faces
*if,GEO_pierCount,gt,0,then
    batch_sliceyz,'GEO_pierX_i'
    batch_sliceyz,'GEO_pierX_j'
*endif

! Perform longitudinal cuts on the deck (footpaths, girders, load trains)
ALLSEL
*do,nthY,1,XZsliceCount,1
    yVal=XZslice(nthY)
    WPSLICEXZ,yVal
*enddo

! Create component with girder lines & keypoints
LSEL,U,LINE,,ALL
*do,nthGirder,1,GEO_girderCount,1
    girderCompName=strcat('COMPL_GIRDER_',chrval(nthGirder))
    yPos=GEO_girderY(nthGirder)
    LSEL,s,LOC,Y,yPos
    cmpadd,girderCompName
    cmpadd,'COMPL_Girder'
*enddo
cmsel,s,COMPL_Girder
KSLL,S
CM,COMPK_Girder,KP

! Create component with gerber beams
lsel,u,line,,all
ksel,u,kp,,all
*if,GEO_gerberStatus,eq,1,then
    *do,nthGerber,1,GEO_gerberCount,1
        x_i=GEO_gerberX_i(nthGerber)
        x_j=GEO_gerberX_j(nthGerber)
        lsel,a,loc,x,x_i,x_j
    *enddo
    cmsel,r,COMPL_Girder
    ksll,s
    cm,COMPL_Gerber,line
    cm,COMPK_Gerber,kp
    cmsel,s,COMPL_Girder
    cmsel,u,COMPL_Gerber
    cm,COMPL_Girder,line
    ksll,s
    cm,COMPK_Girder,kp
*else
    cm,COMPL_Gerber,line
    cm,COMPK_Gerber,kp
*endif
*do,nthGirder,1,GEO_girderCount,1
    yPos=GEO_girderY(nthGirder)
    cmsel,s,COMPL_Gerber
    lsel,r,loc,y,yPos
    lcmName=strcat('COMPL_GERBER_',chrval(nthGirder))
    cm,%lcmName%,line
*enddo

! Create component with load train path lines
LSEL,U,LINE,,ALL
*do,nthLT,1,LOAD_wheelCount,1
    yPos=LOAD_ltWheelOff(nthLT)
    LSEL,A,LOC,Y,yPos
*enddo
KSLL,S
CM,COMPL_LoadTrain,LINE
CM,COMPK_LoadTrain,KP

! Create component with barrier lines
LSEL,U,LINE,,ALL
CM,COMPL_Barrier,LINE
*if,LOAD_barrierStatus,EQ,1,THEN
    *do,nthBarrier,1,LOAD_barrierCount,1
        yOffset=LOAD_barrierOffset(nthBarrier)
        LSEL,A,LOC,Y,yOffset
    *enddo
    CM,COMPL_Barrier,LINE
*endif

! Create component with support keypoints
ksel,u,kp,,all
ksel,a,loc,x,GEO_supportX(1:GEO_supportCount)
*if,GEO_gerberStatus,eq,1,then
    ksel,a,loc,x,GEO_gerberX_i(1:GEO_gerberCount)
    ksel,a,loc,x,GEO_gerberX_j(1:GEO_gerberCount)
*endif
cmsel,r,COMPK_Girder
cm,COMPK_GirderSupport,kp

! Create gerber consoles
lsel,u,line,,all
*if,GEO_gerberStatus,eq,1,then
    *do,nthGerber,1,GEO_gerberCount,1
        x_tip=GEO_gerberX_i(nthGerber)
        *do,x_idx,1,2,1
            *do,nthConsole,1,GEO_girderCount-1,1
                *if,nthConsole,eq,1,then
                    lcmName_1=strcat('COMPL_GERBER_',chrval(nthConsole))
                    cmsel,s,%lcmName_1%
                    ksll,s
                    ksel,r,loc,x,x_tip
                    k_A=kpnext(0)
                *else
                    k_A=k_B
                *endif

                lcmName_2=strcat('COMPL_GERBER_',chrval(nthConsole+1))
                cmsel,s,%lcmName_2%
                ksll,s
                ksel,r,loc,x,x_tip
                k_B=kpnext(0)

                ksel,s,kp,,k_A
                ksel,a,kp,,k_B
                lsel,u,line,,all
                l,k_A,k_B
                cmpadd,'COMPL_Console'
            *enddo
            x_tip=GEO_gerberX_j(nthGerber)
        *enddo
    *enddo
*endif
ksll,s
cm,COMPL_GerberConsole,line
cm,COMPK_GerberConsole,kp

! Create component with gerber support keypoints
ksel,u,kp,,all
cm,COMPK_GerberTip,kp
cm,COMPK_GerberSupported,kp
*if,GEO_gerberStatus,eq,1,then
    ! Gerber tip (cantilever-girder connection)
    ksel,u,kp,,all
    *do,nthGerber,1,GEO_gerberCount,1
        x_i=GEO_gerberX_i(nthGerber)
        x_j=GEO_gerberX_j(nthGerber)
        ksel,a,loc,x,x_i
        ksel,a,loc,x,x_j
    *enddo
    cmsel,r,COMPK_Gerber
    cm,COMPK_GerberTip,kp
    ! Centilever support (over tubular column)
    ksel,u,kp,,all
    *do,nthGerber,1,GEO_gerberCount,1
        x_m=GEO_gerberX_m(nthGerber)
        x_n=GEO_gerberX_n(nthGerber)
        ksel,a,loc,x,x_m,x_n
    *enddo
    cmsel,r,COMPK_Gerber
    cm,COMPK_GerberSupported,kp
*endif

! Create component with left pedestrian footpath areas
*if,GEO_footpathLeft,GT,0,THEN
    ASEL,S,LOC,Y,(-GEO_deckWidth/2),(-GEO_deckWidth/2)+GEO_footpathLeft
    CM,COMPA_LeftFootpath,AREA
*else
    ASEL,U,AREA,,ALL
    CM,COMPA_LeftFootpath,AREA
*endif

! Create component with right pedestrian footpath areas
*if,GEO_footpathRight,GT,0,THEN
    ASEL,S,LOC,Y,(GEO_deckWidth/2)-GEO_footpathRight,(GEO_deckWidth/2)
    CM,COMPA_RightFootpath,AREA
*else
    ASEL,U,AREA,,ALL
    CM,COMPA_RightFootPath,AREA
*endif






