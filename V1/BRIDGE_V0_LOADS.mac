! BRIDGE_V0_LOADS

!! 4.3.2.3 DEAD LOADS                                                                             !!
! Dead loads: gravity
ACEL,,,LOAD_g

! Pavement surface load
ALLSEL
CMSEL,S,COMPE_Slab
cmsel,a,COMPE_ABT_appSlab
cmsel,a,COMPE_colCap
NSLE,S,ACTIVE
SFE,ALL,2,PRES,,LOAD_pav
SFCUM,ALL,ADD

! Barriers on bridge
*if,LOAD_barrierStatus,EQ,1,THEN
   CMSEL,S,COMPN_Barrier
   *get,NodeCount,NODE,,COUNT
   *if,NodeCount,GT,0,THEN
      totalLoad=LOAD_barrierCount*LOAD_barrierValue*GEO_deckLength
      nodalLoad=totalLoad/NodeCount
      F,ALL,FZ,-nodalLoad
   *endif
*endif

! Barriers on abutments
cmsel,s,COMPN_ABT_Barrier
*get,nc,node,,count
*if,nc,gt,0,then
    abtLength=GEO_ABT1_length*GEO_ABT1_status+GEO_ABT2_length*GEO_ABT2_status
    totalLoad=LOAD_barrierCount*LOAD_barrierValue*abtLength
    nodalLoad=totalLoad/nc
    f,all,fz,-nodalLoad
*endif


!! 4.3.2.4 VEHICLE TRAFFIC                                                                        !!
! Vehicle traffic uniform load
ALLSEL
CMSEL,S,COMPE_Lanes
cmsel,a,COMPE_ABT_Lanes
CMSEL,R,COMPE_occupied
NSLE,S,ACTIVE
*get,ECount,ELEM,,COUNT
*if,ECount,GT,0,THEN
   SFE,ALL,2,PRES,,LOAD_vehicleTraffic
*endif

!! 4.3.2.5 PEDESTRIAN TRAFFIC                                                                     !!
! Pedestrian traffic uniform load
ALLSEL
CMSEL,S,COMPE_LeftFootpath
CMSEL,A,COMPE_RightFootpath
cmsel,a,COMPE_ABT_LeftFootpath
cmsel,a,COMPE_ABT_RightFootpath
CMSEL,R,COMPE_occupied
NSLE,S,ACTIVE
*get,ECount,ELEM,,COUNT
*if,ECount,GT,0,THEN
   SFE,ALL,2,PRES,,LOAD_pedestrianTraffic
*endif
ALLSEL

!! 4.3.2.6 WATER FLOW ON COLUMNS                                                                  !!
! Uniform load on submerged column elements
CMSEL,S,COMPE_Submerged
*if,GEO_frameOpt,eq,0,then
    SFBEAM,ALL,2,PRES,-(LOAD_waterFlow*COLUMN_cx)
*elseif,GEO_frameOpt,eq,1,then
    esel,r,cent,y,GEO_deckWidth/2
    sfe,ALL,1,PRES,-LOAD_waterFlow
*endif

!! 4.3.2.7 WIND ON COLUMNS                                                                        !!
! Uniform load on exposed column elements
CMSEL,S,COMPE_Exposed
*if,GEO_frameOpt,eq,0,then
    SFBEAM,ALL,2,PRES,-(LOAD_columnWind*COLUMN_cx)
*elseif,GEO_frameOpt,eq,1,then
    esel,r,cent,y,GEO_deckWidth/2
    sfe,ALL,1,PRES,-LOAD_columnWind
*endif

!! 4.3.2.8 WIND ON SUPERSTRUCTURE                                                                 !!
! Lateral load on the slabs
CMSEL,S,COMPN_Slab
NSEL,R,LOC,Y,-GEO_deckWidth/2
*get,NodeCount,NODE,,COUNT
NodalLoad=LOAD_deckWind*(GEO_barrierHeight+GIRDER_d+SLAB_t)*GEO_deckLength/NodeCount
F,ALL,FY,NodalLoad

!! 4.3.2.9 EARTH PRESSURE (DISTRIBUTED PRESSURE LOADS)                                            !!
*if,GEO_ABT1_status,eq,1,or,GEO_ABT2_status,eq,1,then

    allsel

    ! Calculate global Z coordinate at the surface level of each abutment
    *del,tmp
    *del,LOAD_ABT1_topLevel
    *voper,               tmp, NLIST_x, mult, ABT1_angCoef
    *voper,LOAD_ABT1_topLevel, tmp    , add , ABT1_linCoef

    *del,tmp
    *del,LOAD_ABT2_topLevel
    *voper,               tmp, NLIST_x, mult, ABT2_angCoef
    *voper,LOAD_ABT2_topLevel, tmp    , add , ABT2_linCoef

    ! Calculate soil height above each node
    *del,LOAD_ABT1_h
    *voper,LOAD_ABT1_h,LOAD_ABT1_topLevel,sub,NLIST_z

    *del,LOAD_ABT2_h
    *voper,LOAD_ABT2_h,LOAD_ABT2_topLevel,sub,NLIST_z

    ! Calculate vertical effective pressures
    *del,tmp
    *del,LOAD_ABT1_sigmaV
    *voper,tmp             , LOAD_ABT1_h, mult, MAT_ABT_soil_dens*LOAD_g
    *voper,tmp             , tmp        , add , MAT_Approach_dens*LOAD_g*SEC_ABT_appSlab_t*GEO_ABT1_appSlab
    *voper,LOAD_ABT1_sigmaV, tmp        , add , LOAD_pav+LOAD_vehicleTraffic

    *del,LOAD_ABT2_sigmaV
    *voper,tmp             , LOAD_ABT2_h, mult, MAT_ABT_soil_dens*LOAD_g
    *voper,tmp             , tmp        , add , MAT_Approach_dens*LOAD_g*SEC_ABT_appSlab_t*GEO_ABT2_appSlab
    *voper,LOAD_ABT2_sigmaV, tmp        , add , LOAD_pav+LOAD_vehicleTraffic

    ! Calculate horizontal pressures
    *del,tmp
    *del,LOAD_ABT1_sigmaH
    *voper,tmp             , LOAD_ABT1_sigmaV, mult, MAT_ABT_Soil_Ka
    *voper,tmp             , tmp             , sub , 2*MAT_ABT_Soil_c*sqrt(MAT_ABT_Soil_Ka)
    *voper,LOAD_ABT1_sigmaH, tmp             , max , 0

    *del,tmp
    *del,LOAD_ABT2_sigmaH
    *voper,tmp             , LOAD_ABT2_sigmaV, mult, MAT_ABT_Soil_Ka
    *voper,tmp             , tmp             , sub , 2*MAT_ABT_Soil_c*sqrt(MAT_ABT_Soil_Ka)
    *voper,LOAD_ABT2_sigmaH, tmp             , max , 0

    ! Calculate vertical and horizontal forces
    *voper, LOAD_ABT1_Fv , LOAD_ABT1_sigmaV , mult , NLIST_apportioned
    *voper, LOAD_ABT1_Fh , LOAD_ABT1_sigmaH , mult , NLIST_apportioned
    *voper, LOAD_ABT2_Fv , LOAD_ABT2_sigmaV , mult , NLIST_apportioned
    *voper, LOAD_ABT2_Fh , LOAD_ABT2_sigmaH , mult , NLIST_apportioned

    *if,GEO_ABT1_status,eq,1,then
        ! Apply stress increases on first abutment

        ! Longitudinal wall and wing (Y<0)
        cmsel,s,COMPA_ABT_longWalls
        cmsel,a,COMPA_ABT_wings
        cmsel,r,COMPA_ABT_1
        asel,r,loc,y,-GEO_deckWidth,0
        nsla,s,1
        nslcomp,'F_h','N_n','nc','LOAD_ABT1_Fh',-1
        f, N_n(1:nc), fy, F_h(1:nc)

        ! Longitudinal wall and wing (Y>0)
        cmsel,s,COMPA_ABT_longWalls
        cmsel,a,COMPA_ABT_wings
        cmsel,r,COMPA_ABT_1
        asel,r,loc,y,0,GEO_deckWidth
        nsla,s,1
        nslcomp,'F_h','N_n','nc','LOAD_ABT1_Fh',1
        f, N_n(1:nc), fy, F_h(1:nc)

        ! Transversal wall
        cmsel,s,COMPA_ABT_tranWall
        cmsel,r,COMPA_ABT_1
        nsla,s,1
        nslcomp,'F_h','N_n','nc','LOAD_ABT1_Fh',1
        f, N_n(1:nc), fx, F_h(1:nc)

        ! Pile cap
        cmsel,s,COMPA_ABT_pileCap
        cmsel,r,COMPA_ABT_1
        nsla,s,1
        nslcomp,'F_v','N_n','nc','LOAD_ABT1_Fv',-1
        f, N_n(1:nc), fz, F_v(1:nc)

    *endif

    *if,GEO_ABT2_status,eq,1,then
        ! Apply stress increases on second abutment

        ! Longitudinal wall and wing (Y<0)
        cmsel,s,COMPA_ABT_longWalls
        cmsel,a,COMPA_ABT_wings
        cmsel,r,COMPA_ABT_2
        asel,r,loc,y,-GEO_deckWidth,0
        nsla,s,1
        nslcomp,'F_h','N_n','nc','LOAD_ABT2_Fh',-1
        f, N_n(1:nc), fy, F_h(1:nc)

        ! Longitudinal wall and wing (Y>0)
        cmsel,s,COMPA_ABT_longWalls
        cmsel,a,COMPA_ABT_wings
        cmsel,r,COMPA_ABT_2
        asel,r,loc,y,0,GEO_deckWidth
        nsla,s,1
        nslcomp,'F_h','N_n','nc','LOAD_ABT2_Fh',1
        f, N_n(1:nc), fy, F_h(1:nc)

        ! Transversal wall
        cmsel,s,COMPA_ABT_tranWall
        cmsel,r,COMPA_ABT_2
        nsla,s,1
        nslcomp,'F_h','N_n','nc','LOAD_ABT2_Fh',-1
        f, N_n(1:nc), fx, F_h(1:nc)

        ! Pile cap
        cmsel,s,COMPA_ABT_pileCap
        cmsel,r,COMPA_ABT_2
        nsla,s,1
        nslcomp,'F_v','N_n','nc','LOAD_ABT2_Fv',-1
        f, N_n(1:nc), fz, F_v(1:nc)

    *endif

*endif






